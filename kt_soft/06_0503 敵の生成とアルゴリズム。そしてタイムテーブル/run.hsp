;
; バッファ画面（ウィンドウ1）を用意し、画像を読み込む
;
buffer 1
picload "stg2.gif"

;
; メイン画面（ウィンドウ0）を 480x640 で用意する
;
screen 0, 480, 640, 0, 0, 0

;
; 敵の設定
;
emx = 12                    ; 数
dim ex, emx : dim ey, emx   ; 位置
dim dx, emx : dim dy, emx   ; 移動量
dim ef, emx                 ; 有無（1＝有）
dim enm, emx                ; 種類

; *****************************************************************************************************************
;    メインループ
; *****************************************************************************************************************
*mainLoop

redraw 0                ; 仮想画面を指定
color  0, 0, 0          ; 色に黒を指定
boxf   0, 0, 480, 640   ; 仮想画面を黒で塗りつぶす

;
; *men 敵の初期化
;
gosub *men

;
; 敵の移動処理
;
repeat emx                          ; 「repeat〜loop」間を emx回 繰り返す
    if ef(cnt) < 1  : continue      ; ef(cnt)が1以下（敵が画面にいない）ならばrepeatに戻る
    if enm(cnt) = 1 : gosub *em01   ; enm(cnt)（敵の種類）が1ならば *em01 へ移動
    if enm(cnt) = 2 : gosub *em02   ; enm(cnt)（敵の種類）が2ならば *em02 へ移動
loop

redraw 1            ; メイン画面を更新
wait 1
;await 1            ; これを入れないとパソコンが固まる
gtime = gtime + 1   ; タイムテーブルの為の変数

goto *mainLoop


; *****************************************************************************************************************
;   *men 敵の初期化
; *****************************************************************************************************************
*men
;
; 敵1（下りて来るだけの動き）の生成時間と生成位置 → 敵1の初期化
; 
if gtime = 0   : bx = 0   : by = -64 : gosub *eb01
if gtime = 1   : bx = 416 : by = -64 : gosub *eb01
if gtime = 50  : bx = 80  : by = -64 : gosub *eb01
if gtime = 51  : bx = 336 : by = -64 : gosub *eb01
if gtime = 100 : bx = 160 : by = -64 : gosub *eb01
if gtime = 101 : bx = 256 : by = -64 : gosub *eb01

;
; 敵2（下りながら左右に動く）の生成時間と生成位置 → 敵2の初期化
;
if gtime = 150 : bx = 0   : by = -64 : gosub *eb02
if gtime = 151 : bx = 415 : by = -64 : gosub *eb02
if gtime = 200 : bx = 0   : by = -64 : gosub *eb02
if gtime = 201 : bx = 415 : by = -64 : gosub *eb02
if gtime = 250 : bx = 0   : by = -64 : gosub *eb02
if gtime = 251 : bx = 415 : by = -64 : gosub *eb02
return

; -----------------------------------------------------------------------------------------
;   *eb01　敵1の初期化
; -----------------------------------------------------------------------------------------
*eb01
repeat emx                       ; 「repeat〜loop」間を emx回 繰り返す
    if ef(cnt) > 0 : continue    ; ef(cnt)が0以上(すでに敵が画面にいる）ならばrepeatに戻る

    enm(cnt) = 1                 ; 敵の種類
    ef(cnt)  = 1                 ; 敵の有無（1＝有）
    ex(cnt)  = bx : ey(cnt) = by ; 敵の生成位置
    dx(cnt)  = 0  : dy(cnt) = 2  ; 敵の移動方向
    break                        ; 「repeat〜loop」間のループから、強制的に抜け出す
loop
return

; -----------------------------------------------------------------------------------------
;   *eb02 敵2の初期化
; -----------------------------------------------------------------------------------------
*eb02
repeat emx                       ; 「repeat〜loop」間を emx回 繰り返す
    if ef(cnt) > 0 : continue    ; ef(cnt)が0以上(すでに敵が画面にいる）ならばrepeatに戻る

    enm(cnt) = 2                 ; 敵の種類
    ef(cnt)  = 1                 ; 敵の有無（1＝有）
    ex(cnt)  = bx : ey(cnt) = by ; 敵の生成位置
    dx(cnt)  = 3  : dy(cnt) = 1  ; 敵の移動方向
    break                        ; 「repeat〜loop」間のループから、強制的に抜け出す
loop
return


; *****************************************************************************************************************
;   敵の移動処理
; *****************************************************************************************************************
; --------------------------------------------------------------------------
;   *em01 敵1の移動処理
; --------------------------------------------------------------------------
*em01
;
; 敵1 移動量（X方向）
;
ex(cnt) = ex(cnt) + dx(cnt)

;
; 敵1 移動量（Y方向）
;
ey(cnt) = ey(cnt) + dy(cnt)
if ey(cnt) > 640 : ef(cnt) = 0  ; 画面から消えたら敵の存在を無しにする

;
; 敵1の表示
;
pos ex(cnt), ey(cnt)
gmode 2 
gcopy 1, 0, 64, 64, 64

return

; --------------------------------------------------------------------------
;   *em02 敵2の移動処理
; --------------------------------------------------------------------------
*em02
;
; 敵2 移動量（X方向）
;
ex(cnt) = ex(cnt) + dx(cnt)
if (ex(cnt)<1) | (ex(cnt)>415) : dx(cnt) = -dx(cnt) ; 画面の両端まで行ったら反転させる

;
; 敵2 移動量（Y方向）
;
ey(cnt) = ey(cnt) + dy(cnt)
if ey(cnt) > 640 : ef(cnt) = 0  ; 画面から消えたら敵の存在を無しにする

;
; 敵2の表示
;
pos ex(cnt), ey(cnt)
gmode 2
gcopy 1, 0, 128, 64, 64
return
